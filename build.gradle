
// Apply plugins
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'application'

configurations {
  compile {
    transitive = true
  }

  runtime {
    extendsFrom compile
  }
}

dependencies {
  compile "org.jruby:jruby-complete:1.7.13"
  compile 'javax.enterprise:cdi-api:1.1'
  // compile "com.zaxxer:HikariCP:1.3.8"
  compile "org.yaml:snakeyaml:1.14"
  compile "io.undertow:undertow-core:1.0.1.Final"
  compile "io.undertow:undertow-servlet:1.0.1.Final"
  compile "org.quartz-scheduler:quartz:2.2.1"
  compile "org.jboss.weld.se:weld-se-core:2.2.4.Final"
  compile "org.jruby.rack:jruby-rack:1.1.14"

  testCompile 'junit:junit:4.11'

}

buildscript {
  repositories {
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
  }
}

mainClassName = 'org.stokesdrift.Server'

loadProperties("${System.getProperty('user.home')}/gradle.properties")

defaultTasks = ['assemble', 'copyDependsLibs']

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
mainClassName = "org.stokesdrift.Server"

repositories {
  mavenLocal()
  maven { url 'http://deux.gemjars.org' }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  mavenCentral()
}


sourceSets {
  main {
    // compileClasspath = runtimeClasspath
    resources.srcDirs = ['src/main/ruby', 'src/main/resources']
  }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

test.doFirst {
    copy {
        from 'build/resources/main/META-INF/beans.xml'
        into 'build/classes/main/META-INF/'
    }
    copy {
        from 'build/resources/test/META-INF/beans.xml'
        into 'build/classes/test/META-INF/'
    }
}

signing {
    sign configurations.archives
}

def loadProperties(String sourceFileName) {
  def config = new Properties()
  def propFile = new File(sourceFileName)
  if (propFile.canRead()) {
    config.load(new FileInputStream(propFile))
    for (Map.Entry property in config) {
      project.ext[property.key] = property.value;
    }
  }
}

task copyDependsLibs(type: Copy) {
  destinationDir = new File('build')
  into('libs') {
    from configurations.runtime
  }
}

uploadArchives {
  repositories {
    mavenDeployer {

      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Stokes Drift Server'
        packaging 'jar'
        description 'Service Platform for the modern age'
        url 'http://www.stokesdrift.com'
        scm {
          connection 'scm:git:git@github.com:stokesdrift/stokesdrift.git'
          developerConnection 'scm:git:git@github.com:stokesdrift/stokesdrift.git'
          url 'git@github.com:stokesdrift/stokesdrift.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
          }
        }

        developers {
          developer {
            id 'driedtoast'
            name 'Daniel Marchant'
            email 'driedtoast@gmail.com'
          }
        }
      }
    }
  }
}